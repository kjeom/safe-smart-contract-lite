// SPDX-License-Identifier: MIT

//  Off-chain signature gathering multisig that streams funds - @austingriffith
//
// started from ğŸ— scaffold-eth - meta-multi-sig-wallet example https://github.com/austintgriffith/scaffold-eth/tree/meta-multi-sig
//    (off-chain signature based multi-sig)
//  added a very simple streaming mechanism where `onlySelf` can open a withdraw-based stream
//

pragma solidity >=0.8.0 <0.9.0;
// Not needed to be explicitly imported in Solidity 0.8.x
// pragma experimental ABIEncoderV2;

import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol"; // ì„œëª… ê´€ë ¨ ì‘ì—…

contract SafeLite {
    using ECDSA for bytes32;

    event Deposit(address indexed sender, uint amount, uint balance);
    event ExecuteTransaction(
        address indexed owner,
        address payable to,
        uint256 value,
        bytes data,
        uint256 nonce,
        bytes32 hash,
        bytes result
    );
    event Owner(address indexed owner, bool added);
    mapping(address => bool) public isOwner; // í•´ë‹¹ ì£¼ì†Œê°€ ì†Œìœ ì£¼ì¸ì§€ true/false
    uint public signaturesRequired; // í•„ìš”í•œ ì„œëª…ì˜ ìˆ˜, ì“°ë ˆì‰¬í™€ë“œ?ë¼ê³  ë³´ë©´ë ë“¯
    uint public nonce; // íŠ¸ëœì­ì…˜ì˜ ë²ˆí˜¸?ë¥¼ ì €ì¥?
    uint public chainId; 

    constructor(uint256 _chainId, address[] memory _owners, uint _signaturesRequired) {
        require(_signaturesRequired > 0, "constructor: must be non-zero sigs required"); // 1ê°œ ì´ìƒì˜ ì„œëª…ê°œìˆ˜ê°€ ìˆì–´ì•¼ê² ê³ 
        signaturesRequired = _signaturesRequired; // í•´ë‹¹ ì»¨íŠ¸ë™íŠ¸ì˜ ì“°ë ˆì‰¬ í™€ë“œê°’ ì…ë ¥
        for (uint i = 0; i < _owners.length; i++) { // ì˜¤ë„ˆ ê°œìˆ˜ë§Œí¼, í•´ë‹¹ ì˜¤ë„ˆì˜ ê°œìˆ˜ë§Œí¼, ì†Œìœ ì£¼ ë„£ê³ 
            address owner = _owners[i];
            require(owner != address(0), "constructor: zero address");
            require(!isOwner[owner], "constructor: owner not unique");
            isOwner[owner] = true;
            emit Owner(owner, isOwner[owner]);
        }
        chainId = _chainId;
    }

    modifier onlySelf() { // í•¨ìˆ˜ê°€ ì»¨íŠ¸ë™íŠ¸ ìì²´ì—ì„œ í˜¸ì¶œë˜ì—ˆëŠ”ê°€?
        require(msg.sender == address(this), "Not Self");
        _;
    }

    function addSigner(address newSigner, uint256 newSignaturesRequired) public onlySelf { // ìƒˆë¡œìš´ ì„œëª…ì ì£¼ì†Œ, ìƒˆë¡œìš´ í•„ìš” ì„œëª… ìˆ˜
        require(newSigner != address(0), "addSigner: zero address"); // ì œë¡œ ì£¼ì†Œê°€ ì•„ë‹Œì§€
        require(!isOwner[newSigner], "addSigner: owner not unique"); // ê¸°ì¡´ ì£¼ì†Œê°€ ì•„ë‹Œì§€
        require(newSignaturesRequired > 0, "addSigner: must be non-zero sigs required"); // í•„ìš” ì„œëª… ìˆ˜ëŠ” 0 ì´ˆê³¼
        isOwner[newSigner] = true; // ìƒˆë¡­ê²Œ ì£¼ì†Œ ë“±ë¡í•´ì£¼ê³ 
        signaturesRequired = newSignaturesRequired; // ê¸°ì¡´ í•„ìš” ì„œëª…ê°’ë„ ë³€ê²½í•´ì£¼ê¸°
        emit Owner(newSigner, isOwner[newSigner]);
    }

    function removeSigner(address oldSigner, uint256 newSignaturesRequired) public onlySelf {
        require(isOwner[oldSigner], "removeSigner: not owner");
        require(newSignaturesRequired > 0, "removeSigner: must be non-zero sigs required");
        isOwner[oldSigner] = false; // ê¸°ì¡´ ì£¼ì†Œ ë“±ë¡í•œ ê±° ë°•íƒˆì‹œí‚¤ê³ 
        signaturesRequired = newSignaturesRequired; // ê¸°ì¡´ í•„ìš” ì„œëª…ê°’ë„ ë³€ê²½
        emit Owner(oldSigner, isOwner[oldSigner]);
    }

    function updateSignaturesRequired(uint256 newSignaturesRequired) public onlySelf { // í•„ìš” ì„œëª… ê°’ë§Œ ë³€ê²½í•´ì£¼ê¸°
        require(newSignaturesRequired > 0, "updateSignaturesRequired: must be non-zero sigs required");
        signaturesRequired = newSignaturesRequired;
    }

    function getTransactionHash( // íŠ¸ëœì­ì…˜ í•´ì‹œë¥¼ ê³„ì‚°
        uint256 _nonce,
        address to,
        uint256 value,
        bytes memory data
    ) public view returns (bytes32) {
        return keccak256(abi.encodePacked(address(this), chainId, _nonce, to, value, data));
    }

    function executeTransaction( // íŠ¸ëœì­ì…˜ ì‹¤í–‰, ì†Œìœ ì£¼ì¸ì§€ í™•ì¸í•˜ê³ , ì„œëª…ì„ ê²€ì¦
        address payable to,
        uint256 value,
        bytes memory data,
        bytes[] memory signatures
    ) public returns (bytes memory) {
        require(isOwner[msg.sender], "executeTransaction: only owners can execute");
        bytes32 _hash = getTransactionHash(nonce, to, value, data);
        nonce++;
        uint256 validSignatures;
        address duplicateGuard = address(0);
        for (uint i = 0; i < signatures.length; i++) {
            address recovered = recover(_hash, signatures[i]);
            require(recovered > duplicateGuard, "executeTransaction: duplicate or unordered signatures");
            duplicateGuard = recovered;
            if (isOwner[recovered]) {
                validSignatures++;
            }
        }

        require(validSignatures >= signaturesRequired, "executeTransaction: not enough valid signatures");

        (bool success, bytes memory result) = to.call{value: value}(data);
        require(success, "executeTransaction: tx failed");

        emit ExecuteTransaction(msg.sender, to, value, data, nonce - 1, _hash, result);
        return result;
    }

    function recover(bytes32 _hash, bytes memory _signature) public pure returns (address) {
        return _hash.toEthSignedMessageHash().recover(_signature); // ì„œëª…ì ì£¼ì†Œ ë³µêµ¬?
    }

    receive() external payable {
        emit Deposit(msg.sender, msg.value, address(this).balance);
    }
}
